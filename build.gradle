plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven-publish'
  id 'signing'
  id 'net.researchgate.release' version '2.6.0'
}

repositories {
  gradlePluginPortal()
  jcenter()
}

dependencies {
  implementation localGroovy()
  implementation "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:${testsetsPluginVersion}"

  testImplementation gradleTestKit()
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

test {
  // To use JUnit5 Jupiter
  useJUnitPlatform()

  // Tests summary (displayed at the end)
  afterSuite { desc, result ->
    if (!desc.parent) {
      println "\nTests result: ${result.resultType}"
      println "Tests summary: ${result.testCount} tests, " +
              "${result.successfulTestCount} succeeded, " +
              "${result.failedTestCount} failed, " +
              "${result.skippedTestCount} skipped"
    }
  }
}

gradlePlugin {
  plugins {
    gradleJava {
      id = "com.ekino.oss.gradle.plugin.java"
      implementationClass = "com.ekino.oss.gradle.plugin.java.JavaPlugin"
    }
  }
}

task sourcesJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  classifier "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {

      from components.java

      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'Gradle java plugin'
        description = 'Java plugin applying some configuration for your builds (mavenPublish, testSets, etc ...)'
        url = 'https://github.com/ekino/gradle-java-plugin'
        licenses {
          license {
            name = 'MIT License (MIT)'
            url = 'https://opensource.org/licenses/mit-license'
          }
        }
        developers {
          developer {
            organization = 'ekino'
            organizationUrl = 'https://www.ekino.com/'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ekino/gradle-java-plugin.git'
          developerConnection = 'scm:git:ssh://github.com:ekino/gradle-java-plugin.git'
          url = 'https://github.com/ekino/gradle-java-plugin'
        }
      }

    }

    //needed by sonatype oss check in staging
    mavenPluginMarker(MavenPublication) {
      groupId gradlePlugin.plugins.gradleJava.id
      artifactId gradlePlugin.plugins.gradleJava.id + ".gradle.plugin"
      pom {
        name = 'Gradle java plugin'
        description = 'Java plugin applying some configuration for your builds (mavenPublish, testSets, etc ...)'
        url = 'https://github.com/ekino/gradle-java-plugin'
        licenses {
          license {
            name = 'MIT License (MIT)'
            url = 'https://opensource.org/licenses/mit-license'
          }
        }
        developers {
          developer {
            organization = 'ekino'
            organizationUrl = 'https://www.ekino.com/'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ekino/gradle-java-plugin.git'
          developerConnection = 'scm:git:ssh://github.com:ekino/gradle-java-plugin.git'
          url = 'https://github.com/ekino/gradle-java-plugin'
        }
      }
    }
  }
  repositories {
    maven {
      url = ossrhUrl
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}

task printVersion {
  println project.version
}

signing {
  sign publishing.publications.mavenJava
  //needed by sonatype oss check in staging
  sign publishing.publications.mavenPluginMarker
}
